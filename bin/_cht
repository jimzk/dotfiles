#!/usr/bin/env zsh
#
### Search and render cheatsheets in command line

search_cheatsheets_filename() {
    local query=()
    while read -r line; do
        query+=( "$line" )
    done < <(
        cd $CHEATSHEETS_HOME/cht &&
        fd "" -d 1 --type f --follow --exec basename {} .md \; | sort |
        fzf --query "$@" \
        --prompt 'filename> '
    )
    _execute_cheatsheet "${query[@]}"
}

search_cheatsheets_content() {
    local query=()
    while read -r line; do
        query+=( "$line" )
    done < <(
        cd $CHEATSHEETS_HOME/cht &&
        rg --max-depth 1 -w --color=always --line-number --no-heading --smart-case "${*:-}" |
        sed 's/.md//' |  # cut suffix ".md" from filename
        fzf --delimiter : \
        --prompt 'content> '
    )
    # remove useless part
    query[2]=$(echo ${query[2]} | sed 's/:.*//')

    _execute_cheatsheet "${query[@]}"
}

# input:
#   {query_text}
#   {query_text}, {matched_line}
_execute_cheatsheet() {
    [[ "$#" > 2 ]] && echo "[ERROR] only receive two parameters: {query_text}, {matched_line}" && exit 1

    local query_text="$(echo -e $1 | xargs echo -n)"
    local matched_line="$(echo -e $2 | xargs echo -n)"
    case "$query_text" in
        *\. )
            search_cheatsheets_filename ${query_text:0:-1}
            ;;
        *\? )
            search_cheatsheets_content ${query_text:0:-1}  # cut last char ?
            ;;
        * )
            matched_line=${matched_line%.md}  # remove suffix ".md"
            if [[ -n $matched_line ]]; then
                glow -s $DOTFILES_HOME/config/glow.style.json $CHEATSHEETS_HOME/cht/$matched_line.md
            else
                [[ -n $query_text ]] && ${EDITOR:-vim} $CHEATSHEETS_HOME/cht/$query_text.md
            fi
            ;;
    esac
}

fzf_header=$(cat <<EOF
[foo.] search filename, [foo?] search content; [CTRL-O] edit/create
EOF
)
fzf_preview='expr="bat --style=plain {1}.md"; [[ -z {2} ]] && eval "$expr" || eval "$expr --highlight-line {2}"'
FZF_DEFAULT_OPTS="$FZF_DEFAULT_OPTS --height=90% --info=hidden \
--header '$fzf_header' \
--preview '$fzf_preview' \
--preview-window down,70%,wrap,border \
--print-query \
--bind='ctrl-o:execute(cd $CHEATSHEETS_HOME/cht; [[ -n {1} ]] && ${EDITOR:-vim} {1}.md || ${EDITOR:-vim} {q}.md)+abort'"

LESS='-iMsR -j3'

[[ "$#" > 1 ]] && echo "ERROR: only accpet ONE parameter!" && exit 1
case "$1" in
    -help | -h )
        _print_help "$(realpath $0)"
        exit 0
        ;;
    * )
        ;;
esac
search_cheatsheets_filename "$*"
